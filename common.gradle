//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven'

String mavenGroupId = 'my.geosparql_benchmarking'
String mavenVersion = '1.0-SNAPSHOT'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven {
        url "http://download.osgeo.org/webdav/geotools/"
    }

    maven{
        url "http://maven.geo-solutions.it"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    //SLF4J - Logback Classic
    ext.logbackVersion = "1.2.3"

    //SLF4J
    ext.slf4jVersion = "1.7.25"

    //JUnit
    ext.junitVersion = "4.12"
    testCompile "junit:junit:$junitVersion"

    //System Out Bridge to Logging
    ext.sysoutBridgeVersion = "1.0.2"
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
